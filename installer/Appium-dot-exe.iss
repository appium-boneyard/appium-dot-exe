; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define TargetAppName "Appium"
#ifndef TargetAppVersion
  #define TargetAppVersion "0.0.0.0"
#endif
#define TargetAppPublisher "Appium Contributors"
#define TargetAppURL "http://appium.io"
#define GitHubURL "https://github.com/appium/appium-dot-exe"
#define TargetAppExeName "Appium.exe"
#define TargetAppIcon "..\..\Resources\appium.ico"
#ifndef BuildConfig
  #define BuildConfig "Release"
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{9E863C9E-3A59-4D73-B207-2D91E575AC49}
AppName={#TargetAppName}
AppVersion={#TargetAppVersion}
;AppVerName={#TargetAppName} {#TargetAppVersion}
AppPublisher={#TargetAppPublisher}
AppPublisherURL={#TargetAppURL}
AppSupportURL={#GitHubURL}
AppUpdatesURL={#GitHubURL}
VersionInfoVersion={#TargetAppVersion}
DefaultDirName={pf}\{#TargetAppName}
DefaultGroupName={#TargetAppName}
AllowNoIcons=yes
OutputDir=..\..\..\installer
SourceDir=.\..\AppiumWPF\bin\{#BuildConfig}\
OutputBaseFilename=appium-installer
SetupIconFile={#TargetAppIcon}
UninstallDisplayIcon={uninstallexe}
Compression=lzma2
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\..\..\tools\lmza_sdk_cs\7zr.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall
Source: "node_modules.7z"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall
Source: "*"; Excludes:"node_modules*,*vshost*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
Source: {#TargetAppIcon}; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: "{app}\node_modules"; Permissions: users-full

[Icons]
Name: "{group}\{#TargetAppName}"; Filename: "{app}\{#TargetAppExeName}"
Name: "{commondesktop}\{#TargetAppName}"; Filename: "{app}\{#TargetAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{tmp}\7zr.exe"; Parameters: "x {tmp}\node_modules.7z -o""{app}"" -aoa -y";
Filename: "{app}\{#TargetAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(TargetAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{app}\node_modules"

[Code]
function InitializeUninstall(): Boolean;
  var ErrorCode: Integer;
  begin
    ShellExec('open', 'taskkill.exe', '/f /t /im {#TargetAppExeName}','',SW_HIDE,ewNoWait,ErrorCode);
    ShellExec('open', 'tskill.exe',' {#TargetAppExeName}','',SW_HIDE,ewNoWait,ErrorCode);
    result := True;
end;

